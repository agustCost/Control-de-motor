//Codigo placa slave Id 1 (Medicion)
#include <Arduino.h>
#include <ModbusRTUSlave.h>
#include <ModbusADU.h>
#include <ModbusRTUComm.h>
#include <ModbusSlaveLogic.h>

#define MODBUS_SERIAL Serial2   //pines 16(Rx) y 17(Tx)
#define potPin 25
#define switchPin1 14
#define switchPin2 13
#define adcTempPin 34

//Estos valores se modifican en funcion de la configuracion del master
#define MODBUS_BAUD 38400
#define MODBUS_CONFIG SERIAL_8N1
int MODBUS_UNIT_ID;

ModbusRTUSlave modbus(MODBUS_SERIAL, 18);

static unsigned long lastUpdate = 0;

//se crean las listas de coils, discrete inputs, holding registers e input registers
const uint8_t numDiscreteInputs = 2;
const uint8_t numHoldingRegisters = 2;
const uint8_t numInputRegisters = 2;

// Temperature sensor
float adcValue = 0;
float voltaje = 0;
float temperature = 0;
float gain = 10.15;

uint16_t holdingRegisters[numHoldingRegisters];
bool discreteInputs[numDiscreteInputs];
uint16_t inputRegisters[numInputRegisters];
                                                                    
int vibracion (){
  return (int)(analogRead(potPin) / 40.95);
}

float readTemperature(){
  adcValue = analogRead(adcTempPin);
  voltaje = ((adcValue / 4095.0)) * 3.3;
  float sensorVoltaje = voltaje / gain;
  temperature = sensorVoltaje * 100; // 10mV / C
  return temperature;
}

void setup() {
  //se inicializan los registros y la comunicacion por modbus
  pinMode(potPin, INPUT);
  pinMode(switchPin1, INPUT_PULLUP);
  pinMode(switchPin2, INPUT_PULLUP);
  pinMode(18, OUTPUT);
  digitalWrite(18, LOW); 

  int sw1 = digitalRead(switchPin1);
  int sw2 = digitalRead(switchPin2);

  modbus.configureDiscreteInputs(discreteInputs, numDiscreteInputs);
  modbus.configureHoldingRegisters(holdingRegisters, numHoldingRegisters);
  modbus.configureInputRegisters(inputRegisters, numInputRegisters);

  if(sw1 == 1 && sw2 == 1){
    MODBUS_UNIT_ID = 0;
  }else if (sw2 == 1){
    MODBUS_UNIT_ID = 2;
  }else if (sw1 == 1){
    MODBUS_UNIT_ID = 1;
  }else{
    MODBUS_UNIT_ID = 3;
  }

  MODBUS_SERIAL.begin(MODBUS_BAUD, MODBUS_CONFIG, 16, 17);
  modbus.begin(MODBUS_UNIT_ID, MODBUS_BAUD, MODBUS_CONFIG);
}

void loop() {
  if (millis() - lastUpdate > 100) {
    inputRegisters[0] = vibracion();

    inputRegisters[1] = readTemperature(); // fuxa load

    if(inputRegisters[0] >= holdingRegisters[0]){
      discreteInputs[0] = true;
    }else{
      discreteInputs[0] = false;
    }
    if(inputRegisters[1] >= holdingRegisters[1]){
      discreteInputs[1] = true;
    }else{
      discreteInputs[1] = false;
    }

    lastUpdate = millis();
  }
  modbus.poll();
}
